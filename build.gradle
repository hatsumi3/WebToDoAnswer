  
buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'        
    }
    repositories {
	    maven {
	      url "https://plugins.gradle.org/m2/"
	    }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.junit.platform:junit-platform-gradle-plugin:1.0.0')
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.2.0"
    }
}

apply plugin: "com.github.spotbugs"
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'war'
apply plugin: 'checkstyle'
apply plugin: 'pmd'


group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

compileJava.options.encoding = "UTF-8"

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
}

repositories {
	mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.2.0')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.2.0')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.2.0')
    testRuntimeOnly ('org.junit.platform:junit-platform-launcher')    
    testImplementation('org.junit.platform:junit-platform-commons:1.5.2')
    testImplementation('org.mockito:mockito-core:2.21.0')
    testImplementation('org.mockito:mockito-junit-jupiter:2.21.0')
}

spotbugs {
	ignoreFailures = true
//	sourceSets = [sourceSets.main]
 	toolVersion = '3.1.12'
}
// To generate an HTML report instead of XML
//tasks.withType(com.github.spotbugs.SpotBugsTask) {
//  reports.xml.enabled = false
//  reports.html.enabled = true
//}

jar {
    baseName = "WebToDoAnswer"
    version =  version
}

war {
    baseName = "WebToDoAnswer"
    version =  version
}

checkstyle {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    toolVersion = '7.6.1'
}

//findbugs {
//    // 失敗しても後続の処理を継続させる
//    ignoreFailures = true
//    sourceSets = [sourceSets.main]
//    toolVersion = "3.0.1"
//}

pmd {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

tasks.withType(Pmd) {
    reports {
      xml.enabled = true
    }
}

// CPD（重複コードチェック処理）をCheckタスクに追加
check.doLast {
    File outputDir = new File("$reportsDir/cpd/")
    outputDir.mkdirs()

    ant.taskdef(
        name: 'cpd', 
        classname: 'net.sourceforge.pmd.cpd.CPDTask',
        classpath: configurations.pmd.asPath)

    ant.cpd(
        minimumTokenCount: '100',
        format: 'xml',
        encoding: 'UTF-8',
        outputFile: new File(outputDir, 'cpd.xml')
    ) {
        fileset(dir: "src/main/java") {
            include(name: '**/*.java')
        }
    }
}

javadoc {
    failOnError = false
    // 好みのレベルで
    options.memberLevel = JavadocMemberLevel.PRIVATE
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}

test {
    // 失敗しても後続の処理を継続させる
    ignoreFailures = true
    reports {
        junitXml.enabled = true
    }
}

jacoco {
//    toolVersion = '0.7.5.201505241946'
    toolVersion = '0.8.1'
}

jacocoTestReport {
    reports {
      xml.enabled = true
    }

    // カバレッジレポートからテストクラスを除外
//    afterEvaluate { 
//        classDirectories = files(classDirectories.files.collect {
//            fileTree(dir: it, exclude: ['**/*Test.class']) 
//        })
//    }
}
